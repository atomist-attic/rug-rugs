/*
 * Copyright Â© 2016 Atomist, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@description "adds a new Rug Executor to an existing Rug Archive project"
@tag "rug"
@tag "atomist"
editor AddRugExecutor

precondition IsRugArchive

@displayName "Executor Name"
@description "name of your new executor"
@validInput "a valid Rug Executor name starting with a capital letter and containing only alphanumberic characters from one to 100 characters long"
@minLength 1
@maxLength 100
param executor_name: ^[A-Z][A-Za-z0-9]*$

@displayName "Executor Description"
@description "short descriptive text describing the new executor"
@minLength 1
@validInput "free text"
param description: @any

@displayName "Bot Intent"
@description "the phrase that the bot will use to run this executor"
@minLength 1
@validInput "free text"
param bot_intent: @any

let executor_path = { ".atomist/executors/" + executor_name + ".ts" }
let default_executor_name = "DefaultExecutor"
let default_executor_path = { ".atomist/executors/" + default_executor_name + ".ts" }
let default_executor_function_name = "defaultExecutor"
let executor_function_name = { executor_name.charAt(0).toLowerCase() + executor_name.slice(1) }
let default_bot_intent = { "tags: [\"atomist/intent=do something\"]" }
let full_bot_intent = { "tags: [\"atomist/intent=" + bot_intent + "\"]" }
let default_description = { "description: \"Default Executor\"" }
let full_description = { "description: \"" + description + "\"" }

with Project
  begin
    do copyEditorBackingFileOrFail default_executor_path executor_path
    with File when path = executor_path
      begin
        do replace default_executor_name executor_name
        do replace default_executor_function_name executor_function_name
        do replace default_bot_intent full_bot_intent
        do replace default_description full_description
      end
  end
