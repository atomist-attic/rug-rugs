/*
 * Copyright Â© 2017 Atomist, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@description "adds a TypeScript editor to a Rug archive project"
@tag "rug"
@tag "typescript"
editor AddTypeScriptEditor

precondition IsRugArchive
precondition IsSetUpForTypeScript

@displayName "Editor Name"
@description "name of your new editor"
@validInput "a valid Rug Editor name starting with a capital letter and containing only alphanumeric characters from one to 100 characters long"
@minLength 1
@maxLength 100
param editor_name: ^[A-Z][A-Za-z0-9]*$

@displayName "Editor Description"
@description "description of editor to add to Rug archive project"
@validInput "a string between one and 100 characters long"
@minLength 1
@maxLength 100
param description: @any

let new_editor_name = { ".atomist/editors/" + editor_name + ".ts" }
let new_test_name = { ".atomist/tests/" + editor_name + ".rt" }

with Project p begin
  do copyEditorBackingFileOrFail ".atomist/editors/HelloTypeScript.ts" new_editor_name
  do copyEditorBackingFileOrFail ".atomist/tests/HelloTypeScript.rt" new_test_name
  with File f when path = new_editor_name
    begin
      do replace "HelloTypeScript" editor_name
      do replace "@DESCRIPTION@" description
      do replace "helloTypeScript" { editor_name[0].toLowerCase() + editor_name.slice(1) }
    end
  with File f when path = new_test_name
    do replace "HelloTypeScript" editor_name
end
